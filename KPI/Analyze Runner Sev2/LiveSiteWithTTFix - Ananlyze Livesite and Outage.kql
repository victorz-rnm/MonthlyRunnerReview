https://dataexplorer.azure.com/clusters/icmcluster/databases/IcmDataWarehouse?query=H4sIAAAAAAAAA81Y%2B3PiOBL%2Bnb9Cy1UtZo8ECI8kk2GqMiTZoSpk2ISduapMjlJsAVpsi5XkJOw9%2Fvb7JNvYxsxjb6%2FuLlUJotX6utXd6kd8pokvxOotdVfTgAwIXQinfdLy6mcVH3tTyYN3OvCHItQs1GBwlJav8MvDRb3ytwrBj2FUmq3b2JZs7VOXOdVatUHsny2FGIr5g9N14JujzSa5jfeJCqjU5NdIaKYIDT0SivDgUTK6giii1uBRBXFHeXHfh49qfZZJgDZlEe%2Bm4%2Bs9sAQ343pTAO8UwGkQY3%2BfYB%2BVscHCpDJ670HrFtDMHS3cp2qC1ynjWUPsw%2BoVsPwY6XUC1C0D%2BUypA72k4T6wfgFsEYO9ScB6ZbAFDKeZ%2FCzecQHvT53TszQMrLQ9XocX%2FC%2Fc9iQP%2BPr%2Br28efjDqpYY7ziMG4inxsaaLYqycFmC%2BOziwSAcHeayT%2FViuCAIoVsRrt%2FKAnz6pP%2BdD7zRDuhEyoD7%2FjZEg8jVf467PS44Qt%2FFMtCA0tYEl2XOxhMo%2F4je4YHoUutyDEtc8XJk3yJPvex6iH7Ngw6XaqS61XqtXzeZaSE39w4C7Uigx19wNDnGzJg%2FWzadOMwVUTY9pyn3VxFVSIm7WXIqAVZNrGRFnZpHTEP6kXI40C1Idn4Vcme8j79u19NjTIf0tkszqFii7br4PWXNm4GC3QDWZx7VRLxPwBb3ucEGfTRkNbmjAjF4hPl%2BRPRls7XMYWJkkZ9eW07zST9uLg00yBUeCh%2FO5Q6Wkm5nPwoVeOluAOnlD2o0M8L790CAGLIGJIVIl%2F8LDDQ2NhiNPAdfbgJW7zn2t3ep1T2sNgkW%2F3zaL%2FnGn0zWLk6OT9pFZdI%2F6py2zOO6ddixz%2F6R%2F0otP9bon3XjVPbak006v20nP20W%2F1e7bRbvd7Z5YpHan1%2Bp0ag9JCaC%2Bf0E1hWauHyEypVNL3YUg8rAlxaGLMJEU%2B4cr8IjDZx564lkdhkzX6oeG6ZEq5tSGeEwiBOlHpmcjN5ilka1mH7leTqcvlb%2FjgTDJyFh4fM6ZB%2BEMBs2q1Jbj%2FXMIH8aWQ7ASp2DKui0iXLFgrTcOmVAJMak4xIzdvmNPTCLnkNcDclRBAEI5bQLfjzxG9JKR0XCMT6rJkiqyhsNxeqtBCjfdrBFb%2FyS1a%2F7E7hCmNSIkGan3ETIRO3d1RH0yGJB2bFJgji6MrxPbAk9FAeqfyROPG5JpeVbJWNgLSrBHhjYFW6sMkD9gW6Z5wJy5yTR6tv2e8cGnG%2FwcjMcHnler1zOsVMMknLfGSun1BplTXwEhR9m91v6j8W4ZYEt%2FR9VULXaPx1RstxrZeqTuOv3WLfNxGa8sMLcZH9wljdR5pMUF08zdC5DfTREKtMxi0%2BkVf9lFsMT4YLqcTi%2FD0uUsMeWLl9PpuMw1TnnGOcl4bNeFrFn7ptze25Pb8cq1iDOgk3sRiJNmHIebmhG8luIX3D8XjtCJax%2FezAdX%2FqE2Ck%2BikXuiSfotpWSnyFLPzoQ01Dc2BccU04NSV5%2F7nKoGiQvOUESmPiVffKGYl5Cm0wvz5yYxdeIZa9TG9tljFT0qV%2FK15iJMThZ9vxu5DTKB6QKYnAWJMTLCnaY6gnLpZxzBEIp%2BZCcswYQvxqON1LUw%2BS8CeQydqTfw2VyLCOnWlowlD%2FVhGAUzZCD0SFBWDXqtmAxHAnCxGahlNJ%2BjkdgeKO04dm9fHk9oX0nfI3dsktFH5NKliBTDZurvoTkYXHHme5eoBTxp1nczZZKp4wwY5%2BDPJvCkhzI8cfQMSHWkVMSG5lZCbqqJiCx5%2Fi47UbmYBfTFKQbwD%2FViCm5Y4X9MUvyTqo1nYMSaRx%2FfC4WjcC%2B8zw8opiZxI12Toj6VekWE%2Be%2F%2FoajZiY0sJr41AtRdSNdqKfSHo4LrcwWrUMa%2FMTw%2BX8EThH1p6lYIPaTQ7%2F%2FQXlvdVDokzKEKkSAT19BLsfalQM3ftGiP3A4MMjy32Qqxl2QtUC4wGqbJz2wUh30H2%2BmuLaIWzHu7GWL4W7DYrnnx%2F2vDpj5%2BGy22pkW9sYMCMbPDnrDc015%2BPS5%2Fl4PyYXmJUi5D6m99lUFkA5Sy%2BWHFZoppJzuxA%2Fal4M%2Bg9gT%2FtvUbc2UmT8RB3IEQRwtfPMPmcfmqk%2B8GpIZhRfhPzKvZliTfqORcj%2FDAwNOq17P6n%2FQqO8Nrvt3I6ZLWEJ8qc3vXeNheMI5Z03pXz9f855C9rG1VvkIfg9FQYQysDW8n55NRrbF74OZ28pOwHPFqL9M9Nh7QD0g0BOQOWpBLKdG735qweMcXy4dqoyrZAiHro2nwLeIuyLVYcJf6F1ytyJBcScbu7P%2BVhugxzIb534DZTE4Wzn6MS%2B0VfZTcnaTP40I8hxC88z05XcLYsv0UCRkF10KptH0ZheZu3GWxgDJmDLZ7owtOFyHaGu6qiRQejpUolfwBUjVNKPy5REAYcJwwlEznHPjYC6wZt4uSq2Fvs5987ki6%2FjC6mDBpRs7ZcQ9s6HVh7q2svHfPb8cPmLd%2FjZjS5r8txhOgIRTKUhElBLhz2wYymQTOcPJzOWouXzDbu1yTa%2BGujKbJZ0mB2ygMLVS22pq7ZJf7mMdMnOeLBYKO2sQ8nBTO27O195hNZc2mOI%2FNqfmXxJNpGQjecci42SWuCD3radRQEjBd%2BVxTn7XD%2BbeaGxJyGcVWgix1lNvjtNKUKky5hPzBceK%2FMyp8fUD4NwaC4iiQTQJpB9goVu9CgvwXapxi%2F7QXAAA%3D

let lookBackTm = ago(180d);
let TrimHtmlContent = (str:string)
{
    let step1 = replace("'", "'", replace(" ", " ", str));
    // Replace smart quotes and non-breaking spaces
    let step2 = replace("&nbsp;", " ", step1);
    // Replace HTML non-breaking space entity
    let step3 = replace("&amp;", "&", step2);
    // Replace ampersand entity
    let step4 = replace("&quot;", "\"", step3);
    // Replace quote entity
    let step5 = replace("&lt;", "<", step4);
    // Replace less-than entity
    let step6 = replace("&gt;", ">", step5);
    // Replace greater-than entity
    let step7 = replace("&#39;", "'", step6);
    // Replace single quote entity
    let step8 = replace("<[^>]*>", "", step7);
    // Remove HTML tags
    let step9 = replace("<!--[^>]*-->", "", step8);
    // Remove HTML comments
    let step10 = replace("\\s+", " ", step9);
    // Normalize multiple whitespaces to a single space
    step10
};
let getIncidentLink = (incident:string)
{
    let link = strcat("https://portal.microsofticm.com/imp/v3/incidents/details/", incident, "/home");
    link;    
};
let getRepairItemLink = (workItemId:string)
{
    let link = strcat("https://dev.azure.com/msazure/One/_workitems/edit/", workItemId);
    link;    
};
let getSimpleTeamName = (name: string)
{
    let splitList = split(name, "\\");
    let result = iif(array_length(splitList) > 1, splitList[1], name);
    result
};
let XinyanTeamIds = dynamic(['10549', '10661', '67334', '82812', '42690', '75939', '68685', '105484', '10475', '93543', '82813', '60163', '114480', '135033']);
let allData = cluster('https://icmdataro.centralus.kusto.windows.net').database('Common').Get_Icm_Incidents_WithTTx
| where ModifiedDate > lookBackTm
| where OwningTeamId in (XinyanTeamIds) and isempty( ParentIncidentId) and Severity <= 2
//don't include the ICM that has parrent
| where IncidentType =~ 'LiveSite' or IsOutageActual == 1;
let ICMIDs = allData
| summarize by IncidentId;
allData
| extend CreateDate = todatetime(format_datetime(CreateDate, 'yyyy-MM-dd'))
| extend IsOutage = iif(isempty( IsOutage), false, IsOutage), IsOutageActual = iif(isempty( IsOutageActual), false, IsOutageActual), HasTsg = iif(isempty( HasTsg), 0, HasTsg), IsS360Related = iif(isempty( IsS360Related), 0, IsS360Related), IsAutoDetected = iif(isempty( IsAutoDetected), 0, IsAutoDetected)
| extend TTFix = iif(isempty( TTFix), 0, TTFix), TTEng = iif(isempty( TTEng), 0, TTEng), TTM = iif(isempty( TTM), 0, TTM)
| extend icmLink = strcat('https://portal.microsofticm.com/imp/v5/incidents/details/', tostring(IncidentId), '/summary')
| project IncidentId, Title, CreateDate, ModifiedDate, IncidentType, OwningTeamName = getSimpleTeamName(OwningTeamName), OwningTenantName, OwningContactAlias, RepairCount, RepairClosedCount, TTD, TTN, TTEng, TTFix, TTM, Severity, SubscriptionCount, IsAutoDetected, IsOutageActual, PostmortemTitle, PostmortemStatus, Status, HasTsg, Tags, IsS360Related, S360Link, icmLink
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle     hint.strategy=shuffle (     cluster('https://icmcluster.kusto.windows.net').database('IcMDataWarehouse').IncidentCustomFieldEntries
    | where IncidentId in (ICMIDs) and TeamId in (XinyanTeamIds)
    //and Name == "IssueCategory"
    | summarize hint.num_partitions=50 hint.strategy=shuffle arg_max(ModifiedDate, *) by IncidentId, Name
    | summarize hint.num_partitions=50 hint.strategy=shuffle       Category = max(iif(Name =~ 'IssueCategory', Value, ''))  by IncidentId
)
on IncidentId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').IncidentsSnapshotV2
    | where CreateDate > lookBackTm
    | where IncidentId in (ICMIDs) and isempty( ParentIncidentId)
    | project IncidentId, RootCauseId
)
on IncidentId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').RootCauses
    // find root cause
    | summarize arg_max(ModifiedDate, *) by RootCauseId
    | project RootCauseId, RCATitle = Title, RCADesscription = TrimHtmlContent(Description), IsCausedByChange
)
on RootCauseId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').IncidentBugs
    // get work item
    | where CreatedDate > lookBackTm
    | where IncidentId in (ICMIDs)
    | summarize arg_max(ModifiedDate, *) by IncidentId, ExternalId
    | summarize RepairItems = make_set(ExternalId) by IncidentId
    | project IncidentId, RepairItems
)
on IncidentId
| extend IsMissingRCA = iif (tolower(Status) != 'resolved', 0, iif(isempty(RootCauseId), 1, 0)), IncidentLink = getIncidentLink(IncidentId)
| extend IncidentClass = case(
    Title has "ApiUnexpectedFailures", 'CRPAPI',
    Title has "NRPQos", 'NRPQosAPI',
    Title has "[Qos] Server Side Error Rate High]","regionlevelQos",
    Title has "LogicalDisk C FreeSpace Critical", "Disk",
      Title has "WindowsFabricPartitionDown","PartitionDown",
            Title has "PartitionQuorumLossDetectedInServiceFabric","PartitionDown",      
    Title has "DiagnosticsProd","DiagnosticsProd",
  Title has  "port exhaustion","port",
       Title has "MdmQos","MdmQos",
    Title has "Aegis","Aegis",
  Title has  "LVIDPercent_75","compact",
     Title has "[ARM] requests to", "ARMAPI",
    Title has "NRP PerfCounter", 'NRPCPU',
    Title has "Explicit Locks","Locks",
     Title has "Runner", "Runner",     
          Title has "[RunnerICM Aggregation CP", "Runner", 
    'Other'  // default value if neither condition is met
)
| project IncidentId, Severity, IncidentLink, tostring(RepairItems), IsMissingRCA, IsOutageActual, RCATitle, RCADesscription, IsCausedByChange, Title, CreateDate, ModifiedDate, IncidentType, OwningTeamName, OwningTenantName, OwningContactAlias, RepairCount, RepairClosedCount, TTD, TTN, TTEng, TTFix, TTM, SubscriptionCount, IsAutoDetected, PostmortemTitle, PostmortemStatus, Status, HasTsg, IsS360Related,S360Link, Category, RootCauseId, IncidentClass  
  
  