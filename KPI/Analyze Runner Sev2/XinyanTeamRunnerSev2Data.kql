https://dataexplorer.azure.com/clusters/aznwsdn/databases/AzNM?query=H4sIAAAAAAAAA81Ze2%2FbOBL%2FP5%2BC69tbWT05jh9Jk3ZTIE36MNCkgePuHpDmAlaiZTYS5RPpuO49PvvNkHpQD6ftbg9ogCR8DH8zwxnODKmIKRIlyd1z6t%2FNYnJMaJh0Rwf7gft0J4K5Wcrj1yqOThOhmFBA0JUqfQK%2FXITuzr92CPwgoVRsOYDplC0j6rNux%2Bl4RP8pRgiO4B9Y7QI%2BLu33ydTMExnTVJF%2FrhLFJKEiICIRvQ8po3fAisgl0MgKu6HN7hfxQS6flhxAmiaL17PzNy2wBDTjalMBH1XAaWywf8mwh01sIGGpRLlb0MYVNNRRw73vZHijJp7eiDas%2FQpWZJB%2BzYDGTaCISdlTCyrawA4qYKEBe5aB7TfBQtg4xdKteI8reH8ZHT3N3UBza7E6WCF6QNtDG%2FDX6388u3mE4uUb99hGjJP7zMaKhlVfOarA%2FNTraaRez8Y6bMfykzgGwap4gz0b8P17%2BTfb9Y5KpIskjWnEPzMSryLFl6DresHBxbU%2FE5UQmu%2BBHtLrDIed%2F5gzGDI1ET4PQIg3XNzhGeRZv%2BUgRoYEJnyqup2FUkv5pN9fJqmi0W7M%2FTSRyVxxP94Fzfo8XvbvR%2F0cUPYDpiiPZB9UyQdBs%2F4iiVknU0uzgB9LQDAn5elEsTgXcZ2kd9ifBF8vZMDud%2BnnVcq0aLHU7f5bwfq3CAfbFss%2BC7hC6UoG28W6AvUiNmM0vqAxQ7EE%2FH9CWuLXMuKwvRJDnG5rSjyj7wu1gSxlEswINJzPuzRN6eY2YiJUi24B4JJnZOCVgNeDG48gWAZjIHIh%2F87FhgqUcBJIwA02QMr97rUz2NsfHzkegcbBwQAbB49HozE2DoeHgyE2xsODoz1sPN4%2FGmnig8ODw32zan98ODat8WM9dDTaH4%2Fy9bpxsDc40I3BYDw%2B1EiD0f7eaOTcZAlguhKCpUY%2BEM8ZHY3Hjx177mUKuqExLhdUsqoOUzYHdRenEQc3miV3TJzIjfAdz7lM2Rkcn2SDZ%2BvEVzwRsmBazrRgnuoQNA0BZCK44jQqyWFs%2BkoTAB52WGDP%2FQYnMYDFJ9PzGQPPgKbFE%2F6CU0xDzbPCUs%2Bx6SuAyJphsW5yej45Qwn9aAXnNu06cLSytuPuAjv6AfC6zsQ%2FP4PO7zRli2QlGUzmx1ru%2FBuiAksZMcoBHQMvKhNzOZ%2BkKYu0emiP%2FxLn6uzC2EH2XnN1mqxAU5KkZMYVxhQFSVWuuVqQzrWhA4HJSQiBPNQw5PTyprMDkSrVs9BFlYChXMWQkTFyLbhQu2IV3y4BjGtTHQ%2F3zDCcJBA23BzLxWo%2BB440DW9jLrrnScDnnAWoi0ceueTDhuT6TgJkCOeT0CgCYXkoaJSxNXpesXuWQi4gx8dkiMSS3Q%2BRotyqt2sB5so8kwvSrRwlt6JBhfdTxJtzyNTIvYh8O3%2FGgFeCLuUiUb8Nv9qUpUBaeuNHrq58uATvVJsuuQReQpWULorOPoF0ASNqwXBHyILeYymz1LQADw3wX2N%2FbflHxDGmJxc6%2BjnZydUxETosBJM%2BKVoEWhMBniN8izgfgQF9QJ7kjS0cr3MfYzfXDg7gPxwzTAawvE%2ByEVua6mDJ9QbGH23jtdWzyXWpXcl8iHAoFW6nLSj9PO%2B9O5uerWh0pcBcVyy95z6b0bB3etmbXpzfXK9kkAQ%2B7HQKLtsnJ58vGES2JJLZKdx9F6QXMixXAhHA1kdtmMs0CW7I%2Ft5fib%2FgUQBmJO%2FEgtFILTagMfsEpXdg28ykHy4FFE3aUco517MIvdr2FpPFtm5ByucbC7wW27iWjMaBmqh6HNF0w6vP247henYPSOfbSIcV0iEkL8d1LWnyQF5jpodhqf4Piwr7B3rEcb9uz8nWTUfCbwq%2FUGZsm8S5d%2BJOJGvRsXV7CUUaFEezzTIXzWiLoaQ1L7sFE6N4x6utqiddXFCOtZBX8qWhxtxIpq8qxD5cHSkXknRqG42qtQzNmETcXEPQWuv9MQGucGcLjiM2V8kKorMup9qS0%2F625NTFFX8mUz9fZReLLH%2Fh%2FhKsS%2FVgNfQHzdhvE7XH%2F4ykTF06n9JPX8inHnkBfpFCGoXcWocoi3OsU2J6x24lU91yRT05G4BlmnxkvqpwsaB23B3wUGvVj2KjMyb9lC81G9tWdKWSnl8WUBVr%2FAlbZTaHG3HIgue6ZAHx4hNgZ5VrTkadnd8mAZhm0DAdWr5J%2BvX2amUDoLfNiR%2FWnq%2FhEpWkm9yUunIDWcgiWZMA4mKYUqiG1lSCYgFrMcp3su0p6gXFKt5HoPC%2B7ryAm2jH6%2BS16lkuS%2BfG2XqMS4laiuKKhzRgrVhvORtIcnpxLJjSN2QR9tDN4XpLQ1bepfFih%2BbuzfSE47Wg4HxsXBXvD9XJEFHlvZ8DZYTQO5tOIOt%2BQXRE0Szblfrp2OB47Up7pTDuA77ewnUL4A%2Fn6tMkUacU2rLi5SkMEx%2FHvyktFGiN0GDNQCw%2FPTFVyrEpp%2FUIBM8ieuJE9f23a8VW1%2Byizez%2Fs430s1jLQFT2EMfiQMLYOQNn92dQesTQqkaJ7ErL82p3HtF7cG19xVISdvNDoU2xVebEC13ygVu%2BGL7ICntTCxl2TnZNqxeDLzUDtzF7VeG0PclfpsyHy9%2BMxwzuH%2FGysGil4MYqs27YBkFZa1eZexVRM5htZvtm0%2BFPN19ZmlCky4r5FNR4MPCKqTcU2%2BadDp0sTaJLqhZGRhiY8xBimNkx8LafUb7dprZoq59THi7aJqvByVYfl7WX9Zklq7tV7eFNpbq3LZeEmuWb1qj2B03faDV%2FXa4qijmZbXv18D6VAbyuVk3KkjKla7jNYl2pL%2Ba3H2Uiujhi36IaYaWx51UK2KZ64GlSOPZFbXvGqRijSeVuyRvORf%2FEaZu0svCD0Ei3bX0Ov%2FNwOVjn0VIItlV4HtmzkaXJ%2BW3OacVuQBp4zVftzLz4pr2nKfZc%2FQYF2ZqoDDd%2Fh9LXXCo2JJnjmLn6BSZi7G4XyOofI4eib8SpPPMdFy%2FQtjB8XsjAJYhFhZxDEA%2Fw60qWAhSs95BKQOQ3VJrHH5frdbJ%2ByT8xUzQ6M2TK9Ssz5IzsYuW4NSlx1%2BBU38EqqFaLNd73kqlbPBxa9zQXq7mKUauzgLRXF7R8WpTk9wkIKxOf45X2D%2B9hu8BtxxaLQTwdNZEy6xYFf4CB0appPVSzLE2xB9v90Naec4lf3iCygLRaHJVEyRryFeRetYKtQVFSJpPongUgz17%2BbpU9almnR2f877P9sRELY6IlraC%2B4vdsIs7oRubFtBZT1%2BdmFmTEzxsKyofbAN%2FNnIBuYFAk6y7wsGtFtxolrvCh6u38d8bMdznoJfM19LrlM7ZFXh7K7G2s8a2tWyWxX644HA0cf5Umq2WmSv2U25%2BdvOL5GvtOS1hovv7DGjOQr3HeguMaBKgaMcFA2WE%2FEGZt2Mu2r0TetuEvvqE55RjBL0jf9oxmxEaJCW7hlit%2B9s219qG4a3038Cr%2BXjmHluk966NF1V2wJzhcw3UBCQUiAGEzN4OgRhi7b%2BoU21BezXHyPiZ58OCTiFNZpkwtGji4V%2FN%2Br7iutN1mGrcYLITlEnIQ%2F1C6Z2MQITDTVV9y64%2FfeUmdPR0XPvRwOebZr7XtGTvPJrDPkDBDuuX6VU6CReFCrj9jd%2B148i3XYK92FvGTWZIqDKzWxysq%2Ff8BZ1CQmyYkAAA%3D

let lookBackTm = ago(365d);
let TrimHtmlContent = (str:string)
{
    let step1 = replace("'", "'", replace(" ", " ", str));
    // Replace smart quotes and non-breaking spaces
    let step2 = replace("&nbsp;", " ", step1);
    // Replace HTML non-breaking space entity
    let step3 = replace("&amp;", "&", step2);
    // Replace ampersand entity
    let step4 = replace("&quot;", "\"", step3);
    // Replace quote entity
    let step5 = replace("&lt;", "<", step4);
    // Replace less-than entity
    let step6 = replace("&gt;", ">", step5);
    // Replace greater-than entity
    let step7 = replace("&#39;", "'", step6);
    // Replace single quote entity
    let step8 = replace("<[^>]*>", "", step7);
    // Remove HTML tags
    let step9 = replace("<!--[^>]*-->", "", step8);
    // Remove HTML comments
    let step10 = replace("\\s+", " ", step9);
    // Normalize multiple whitespaces to a single space
    step10
};
let getIncidentLink = (incident:string)
{
    let link = strcat("https://portal.microsofticm.com/imp/v3/incidents/details/", incident, "/home");
    link    
};
let getRepairItemLink = (workItemId:string)
{
    let link = strcat("https://dev.azure.com/msazure/One/_workitems/edit/", workItemId);
    link    
};
let getSimpleTeamName = (name: string)
{
    let splitList = split(name, "\\");
    let result = iif(array_length(splitList) > 1, splitList[1], name);
    result
};
let XinyanTeamIds = dynamic(['10549', '10661', '67334', '82812', '42690', '75939', '68685', '105484', '10475', '93543', '82813', '60163', '114480', '135033']);
let RunnerTeamId = '39447';
let RunnerFrameworkPhases = dynamic(['RefreshClientTokenAsync','PreDeploymentActions']);
let DeploymentPhases = dynamic(['CreateRg','InitialDeployment','RGCreation','Redeployment','ValidateARMTemplate']);
let DeletingRgPhase = dynamic(['DeleteRG','DeleteRg']);
let ICMIDs = cluster('icmcluster').database('IcMDataWarehouse').Incidents
| where CreateDate > lookBackTm
| where CorrelationId =~ 'SDNRunners-HitCount' or Title startswith "[RunnerICM Aggregation CP]"
// runner CP ICM
| summarize hint.num_partitions=20 hint.strategy=shuffle arg_min(ModifiedDate, *) by IncidentId
// get all orignal ICM
| where Severity == 2
// sev2 ICMs
| where OwningTeamId in (XinyanTeamIds)
| summarize by IncidentId;
// find all incidents
cluster('icmcluster').database('IcMDataWarehouse').IncidentsSnapshotV2
| where CreateDate > lookBackTm
| where IncidentId in (ICMIDs) and isempty( ParentIncidentId)
// exclude the ICM having parent
| parse Title with * 'Runner Name: ' RunnerName ' Region: ' Region  ' Instance: ' RunnerInstance ' Phase: ' Phase
| parse Title with * '[Aggregate][' * '][' AggrRegion1 ' / ' AggrRunnerName ' / ' AggrRunnerInstance '] ' *
| parse Title with * '[RunnerICM Aggregation CP] [Region: ' AggrRegion2 '] [' *
// [Aggregate][azf-UDRDualStackServiceTag-CP-RNM][usdodcentral / AzNetToolsRunners.UdrNsgServiceTag / azfUdrNsgServiceTagusdodcentralProd] 50% children Unhealthy
| extend RunnerName = iif(isnotempty(RunnerName), RunnerName, AggrRunnerName), RunnerInstance = iif(isnotempty(RunnerInstance), RunnerInstance, AggrRunnerInstance)
| extend Region = iif(isnotempty(Region), Region, iif(isnotempty( AggrRegion1), AggrRegion1, iff(isnotempty( AggrRegion2), AggrRegion2, '')))
| extend Phase = iif(isnotempty(Phase), Phase, 'AggregatedPhase')
| extend RunnerName = iif(isnotempty( RunnerName), RunnerName, iif(Title startswith "[RunnerICM Aggregation CP]", "RunnerICM Aggregation CP", "Unknown"))
| extend FailureType = iif(Phase in (RunnerFrameworkPhases), "Runner Phase", iif(Phase in (DeploymentPhases), "Deployment", iif(Phase in (DeletingRgPhase), "Delete RG", iif(Phase contains "AggregatedPhase", "AggregatedPhase", "Testing Failure"))))
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').IncidentBugs
    // get work item
    | where CreatedDate > lookBackTm
    | where IncidentId in (ICMIDs)
    | summarize arg_max(ModifiedDate, *) by IncidentId, ExternalId
    | summarize RepairItems = make_set(ExternalId) by IncidentId
    | project IncidentId, RepairItems
)
on IncidentId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').IncidentDescriptions
    // get auto-correlation
    | where Date > lookBackTm
    | where IncidentId in (ICMIDs)
    | where ChangedBy == 'IcmAutoCorrelation'
    | extend IcmAutoCorrelation = 1
    | summarize max(IcmAutoCorrelation) by IncidentId
    | project IncidentId, IcmAutoCorrelation = max_IcmAutoCorrelation
)
on IncidentId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').IncidentHistory
    // find out how downgrade was made
    | where ChangeDate > lookBackTm
    | where IncidentId in (ICMIDs)
    | where ChangeCategories =~ '["Edit","SeverityDowngrade"]'
    | summarize arg_max(ChangeDate, *) by IncidentId
    | extend SeverityDowngradeType = iif(ChangedBy =~ 'CN=networking-autotriage.azure.com', 'Auto-Triage', iif(ChangedBy =~ 'Automation' or ChangedBy =~ 'gautosvc', 'Automation', 'DRI'))
    | extend SeverityDowngradeBy = iif(SeverityDowngradeType != 'DRI', SeverityDowngradeType, ChangedBy)
    | project IncidentId, SeverityDowngradeBy, SeverityDowngradeType
)
on IncidentId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('icmcluster').database('IcMDataWarehouse').RootCauses
    // find root cause
    | summarize arg_max(ModifiedDate, *) by RootCauseId
    | project RootCauseId, RCATitle = Title, RCADesscription = TrimHtmlContent(Description)
)
on RootCauseId
| join kind=leftouter
    hint.num_partitions=50 hint.strategy=shuffle (     cluster('aznwsdn').database('aznwmds').MetricTelemetry
    // find runner instance flavor and its subscriptionId
    | where name == 'E2ERunnersDeployMetric' and isnotempty( RunnerFlavor) and isnotempty( SubscriptionId)
    | summarize arg_max(PreciseTimeStamp, *) by RunnerInstanceName
    | project RunnerInstanceName, Region, SubscriptionId, RunnerFlavor
    | join kind=leftouter
        hint.num_partitions=50 hint.strategy=shuffle     (         cluster('nrp').database('test').GetLatestSimpleControlPathRunnerConfigure)
    on $left.RunnerInstanceName == $right.RunnerInstanceName
    | extend RunnerFlavor == iif(isnotempty( RunnerFlavor), RunnerFlavor, RunnerFlavor1), SubscriptionId = iif(isnotempty( SubscriptionId), SubscriptionId, SubscriptionId1)
    | summarize by RunnerInstanceName, RunnerFlavor, SubscriptionId
)
on $left.RunnerInstance == $right.RunnerInstanceName
| extend SubscriptionId = SubscriptionId1
| extend rawTags = parse_json(Tags)
| extend RCADesscription = iif(isnotempty( RCADesscription), TrimHtmlContent(RCADesscription), '')
| extend SeverityDowngradeBy = iif(isnotempty( SeverityDowngradeBy), SeverityDowngradeBy, 'N/A'), SeverityDowngradeType = iif(isnotempty( SeverityDowngradeType), SeverityDowngradeType, 'N/A')
| extend IcmAutoCorrelation = iif(isnotempty( IcmAutoCorrelation), IcmAutoCorrelation, 0)
| extend IsTriaged = iif(isnotempty( RootCauseId), 1, iif(array_length(rawTags) > 0, 1, 0))
// DRI triaged the ICM with any of these conditions.
| extend IsTriaged = iif(IsTriaged == 1, IsTriaged, iif(OwningTeamId == '39447', 1, 0))
// if the ICM is transfered to runner team, then it is triaged.
| extend IsTriaged = iif(IsTriaged == 1, IsTriaged, iif(HowFixed in ('Transient', 'External'), 1, 0))
// if DRI marked as Transient, it is triaged.
| extend IsTriaged = iif(IsTriaged == 1, IsTriaged, iif((isempty( RepairItems) or array_length( RepairItems) == 0), 1, 0))
// if the ICM has WI associated, then it is triaged.
| extend IsTriaged = iif(IsTriaged  == 1, IsTriaged, iif(SeverityDowngradeBy != 'N/A', 1, 0))
// if ICM is downgraded by Auto-Triage, or Automation or DRI, it is triaged.
| extend IsMissingRCA =  iif(tolower(Status) != 'resolved', 0, iif(isempty(RootCauseId) and (isempty( RepairItems) or array_length( RepairItems) == 0), 1, 0))
// missing RCA
| extend InactiveInDays = iif(Status =~ 'active', datetime_diff('day', now(), ModifiedDate), 0)
| extend StartOfWeek = startofweek(CreateDate)
| extend OwningTeamName = getSimpleTeamName(OwningTeamName)
| extend FinalTeamGroup = iif(OwningTeamId == RunnerTeamId, 'Runner Team', iif(OwningTeamId in (XinyanTeamIds), 'Xinyan Team', 'Other Teams')), TestPhase = iif(Phase =~ 'ValidateARMTemplate', 'ValidateARMTemplate', iif(Phase in (DeploymentPhases), 'Deployment RG', iif(Phase in (DeletingRgPhase), "Delete RG", 'Others')) )
| project IncidentId, IcmLink = getIncidentLink(IncidentId), IsMissingRCA, IsTriaged, StartOfWeek, CreateDate, ModifiedDate, MonitorId, RoutingId, OwningTenantId, OwningTenantName, OwningTeamId, OwningTeamName, OwningContactAlias, Severity, Status, InactiveInDays, Title, RootCauseId, RCATitle, RCADesscription, ResponsibleTeamName, ResponsibleTeamId, Tags, RunnerName, RunnerInstance, Region, Phase, TestPhase, RunnerFlavor, SubscriptionId, FailureType, IcmAutoCorrelation, HowFixed, Mitigation = TrimHtmlContent(Mitigation), tostring(RepairItems), SeverityDowngradeBy, SeverityDowngradeType, FinalTeamGroup
| sort by CreateDate asc  