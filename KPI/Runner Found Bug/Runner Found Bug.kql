https://dataexplorer.azure.com/clusters/icmcluster/databases/IcmDataWarehouse?query=H4sIAAAAAAAAA7VWbW%2FiOBD%2B3l%2Fh40vCKQ2kBAq315O67VaHdLTVgq4nVajyxkPwNrGR7ZSjWu1v33FCSALt9nalQ0i48%2FrMzGNPEzAkkfLxPY0eZyk5IzSWbm%2FQZ%2B13Rwnq%2FuFiQ8UMaDpmGtVsI2jKI%2FfeCbr9cOR4BA%2BDQWAPg9NeL7SH4ckwOLGH8GQw6trDaX%2FUy40Hw8GwX3j1w2FYnMLTXDTq9cNe6Z8fBt1gkB%2BCIAyHeaSg1%2B%2F2es4c8XU6hIsoyRgwojIhQBGDQAlnqLLgP%2BbCAjxid3qjMDx1isLGF5Pxpa0IA2gDynWWxqz0b50Oj9KtzH%2FEH%2BmvuWByrX0Bxmn7jBr6iWpwnXE0ucQ%2F7qiCpcw0oHIsIs5AGH30hayXoIBcKKAG0A7IH7VOV3qpFCTUcCksyK%2FEmV5eF8D18Z%2FcXMhMGIdIRWbcJEC0ocroNTdL0rov7LAUch7HCuI8DLm4nbcwvs7SlCr%2BDGTJhfFFlj6s0JdbG3120i3E2ijEFm%2FO9DJbLDABVfFDyoU7kYwvODAL3SO%2FtsmnDSnLG7Md%2Fpu14CLe9pgL4jYY07bA3R9C3m5AbyQtJnc3%2Fh8H9z6Lq9lVmfPKCsq0kVxfyErJzxAZ8uFfTCdokneEcW2QkXXpu6MDmAHoQ3ydBsDz50zBJTzdrDTK76R6HBuoOHOjYir4c96za5oCOUNyp5paL2dnVbpt0edts80FBCcY%2BaylsF3ATiItNDxYgWslLxhdcUjy%2B18K7luLXNSaV8YF1XPCoKWRSC2khtuMcd%2BabrAfqV%2Bzbs3bHrniguvlW84THimp5cL4f09nU38aLYFlCVr6lX8e7lZxqbjZ%2FECwC5mmUvilZx7mRvGY4yQ%2F4GTTn8a2HyUPjelWyGdgdlA%2FF7cRAoNWszjH7t5SvGpvD6I0zUEhYVTOq%2F%2Fo3LDPI5xrzWMBbCZrdNl6OWXKnY0zv3fw2qwSurFMduYemeELBOYNIjivN6Xyd%2Bod2QuLlw1wGOBWcjuTPQrvrGqKQ67urCp5LfGEa10msXTkfOHWLMnvRMi12ya0CbIU4wPKNaQrs3FreQvznaJWRKGZGhvjF3vxnUspwPGcj5DKJ2AO4g880q0hbJSzkAop%2BvBCTbh9N%2Fg5nkyOWR6m0dJ9v0p54Nfs8r5jTXvgiTVQbhfOG3fmO%2FRohGgwpNhJV7hwGa6B7aRe4f2Mxvj2kUgKg9E0aTWdWx5pGZWB%2FV3QREPLviW7Vfk95NsE15JBcSNe47BF9xIBMGUwOu0edwP8trymqjHqRoQat%2FYjNFTlWvmLCzsBrCKiptpu2zXkP3GdYeEmY1z6kUw7NwI6D2t05uisO8A4Lj2vtqVsneVaraReQWWv%2BPenwZ06sHqZXvPK1Z%2Bkai8cPu17L%2FIe2bzdk7r3SHp7vKnPudmtbydP51BsCwAA

let lookBackTm = ago(365d);
let XinyanTeamIds = dynamic(['10549', '10661', '67334', '82812', '42690', '75939', '68685', '105484', '10475', '93543', '82813', '60163', '114480', '135033']);
// included runner team id
//let RunnerTeamId = '39447';
let ICMIDs = cluster('https://icmcluster.kusto.windows.net').database('IcMDataWarehouse').Incidents
| where CreateDate > lookBackTm
| where CorrelationId =~ 'SDNRunners-HitCount' or Title startswith "[RunnerICM Aggregation CP]"
| summarize hint.num_partitions=20 hint.strategy=shuffle arg_min(ModifiedDate, *) by IncidentId
| where OwningTeamId in (XinyanTeamIds) or (Title startswith "[RunnerICM Aggregation CP]")
| summarize by IncidentId;
let WIIDs = cluster('https://icmcluster.kusto.windows.net').database('IcMDataWarehouse').IncidentBugs
| where IncidentId in (ICMIDs)
//| project ExternalId
| distinct ExternalId;
cluster('https://1es.kusto.windows.net/').database('AzureDevOps').WorkItem
| where OrganizationName == 'msazure'
| where WorkItemId in (WIIDs)
| extend jsonData = parse_json(Data)
| extend jsonDataFields = jsonData["fields"]
| extend CreatedDate = tostring(jsonDataFields["System.CreatedDate"]), FinishDate = tostring(jsonDataFields["Microsoft.VSTS.Scheduling.FinishDate"]), Priority = tostring(jsonDataFields["Microsoft.VSTS.Common.Priority"]), OriginalEstimate = tostring(jsonDataFields["Microsoft.VSTS.Scheduling.OriginalEstimate"]), CompletedWork = tostring(jsonDataFields["Microsoft.VSTS.Scheduling.CompletedWork"])
| extend AreaPath = tostring(jsonDataFields["System.AreaPath"]), IterationPath = tostring(jsonDataFields["System.IterationPath"]), AssignedTo = jsonDataFields['System.AssignedTo']['displayName'], TargetDate = tostring(jsonDataFields['Microsoft.VSTS.Scheduling.TargetDate'])
| extend TargetDate = todatetime(TargetDate), CreatedDate = todatetime(CreatedDate), FinishDate = todatetime(FinishDate)
| extend MissTargetDay = iif((FinishDate < now() and TargetDate < now() or isempty( FinishDate) and isempty( TargetDate)) and State !in ('Done','Removed'), 1, 0)
| extend FinishDate = format_datetime(FinishDate, 'yyyy-MM-dd'), TargetDate = format_datetime(TargetDate, 'yyyy-MM-dd'), CreatedDate = format_datetime(CreatedDate, 'yyyy-MM-dd'), RemainingWork = tostring(jsonDataFields['Microsoft.VSTS.Scheduling.RemainingWork'])
| extend RunnerFoundBug = iif(jsonDataFields["System.Tags"] contains "RunnerFoundBug", "true", "false"), OwningTeam = tostring(jsonDataFields['System.NodeName'])
| extend TargetDate = iif(isempty( TargetDate), "1970-01-01", TargetDate), FinishDate = iif(isempty( FinishDate), "1970-01-01", FinishDate), WorkItemLink = strcat('https://msazure.visualstudio.com/One/_workitems/edit/', WorkItemId)
| project WorkItemId, State, Title, CreatedDate, FinishDate, TargetDate, MissTargetDay, AssignedTo, Priority, OriginalEstimate, CompletedWork, RemainingWork, AreaPath, IterationPath, RunnerFoundBug, OwningTeam, WorkItemLink  

  